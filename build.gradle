plugins {
    id 'java'
    id 'application'
    id 'org.beryx.jlink' version '2.24.4'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com/" }
    maven { url "https://jitpack.io" }
}

ext {
    junitVersion = '5.8.1'
    btVersion = '1.1.0' // Replace with the actual version
    javafxVersion = '17.0.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

application {
    mainModule = 'bl00dy_c0d3_.echovr_installer'
    mainClass = 'bl00dy_c0d3_.echovr_installer.EchoVRInstaller'
    applicationDefaultJvmArgs = [
            "-Djava.library.path=libs/",
            "--add-opens=java.base/java.lang=ALL-UNNAMED",
            "--add-opens=java.base/java.util=ALL-UNNAMED",
            "--add-opens=java.base/sun.nio.ch=ALL-UNNAMED",
            "--add-reads=jlibtorrent=java.logging"
    ]

}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    implementation files('libs/jlibtorrent-1.2.19.0.jar')
    implementation files('libs/jlibtorrent-linux-1.2.19.0.jar')
    implementation "org.openjfx:javafx-controls:${javafxVersion}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}"
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Main-Class": "bl00dy_c0d3_.echovr_installer.EchoVRInstaller"
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task copyLibjpeg(type: Copy) {
    from 'src/main/resources/libjpeg.so.62'
    into "$buildDir/jpackage/EchoVR_Installer/lib/runtime/lib/"
}

task copyJLibTorrent(type: Copy) {
    from 'libs'
    into "$buildDir/jpackage/EchoVR_Installer/bin/libs"
}

task copyTorrentFiles(type: Copy) {
    from 'torrentFiles'
    into "$buildDir/jpackage/EchoVR_Installer/torrentFiles"
}

jlink {
    jpackage {
        appVersion = '0.4'
        installerName = 'Echo_VR_Installer'
        def os = org.gradle.internal.os.OperatingSystem.current()
        if (os.isWindows()) {
            icon = 'C:\\Users\\Marcel\\Documents\\Java\\EchoVR_Installer\\icon.ico'
            installerOptions = [
                    '--app-version', '0.4',
                    '--vendor', 'bl00dy-c0d3',
                    '--name', 'Echo_VR_Installer',
                    '--install-dir', 'EchoVRInstaller',
                    '--icon', 'C:\\Users\\Marcel\\Documents\\Java\\EchoVR_Installer\\icon.ico',
                    '--win-dir-chooser',
                    '--win-shortcut',
                    '--win-menu',
                    '--win-menu-group', 'EchoVRInstaller'
            ]
        } else {
            icon = 'icon.png'
            tasks.named('jpackage').configure { finalizedBy(copyLibjpeg) }
            tasks.named('build').configure { dependsOn(jlink) }
        }
    }
}


tasks.jlink.finalizedBy(copyJLibTorrent)
